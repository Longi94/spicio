apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'io.fabric'

def getKey(Properties properties, String name) {
    return properties.containsKey(name) ? properties.getProperty(name) : "DUMMY_KEY"
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.tlongdev.spicio"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary = true

        Properties keys = new Properties()
        if (project.rootProject.file('keys.properties').exists()) {
            keys.load(project.rootProject.file('keys.properties').newDataInputStream())
        }

        buildConfigField("String", "TRAKT_API_KEY", "\"${getKey(keys, "TRAKT_API_KEY")}\"")
        buildConfigField("String", "GOOGLE_WEB_CLIENT_ID", "\"${getKey(keys, "GOOGLE_WEB_CLIENT_ID")}\"")

        resValue("string", "FACEBOOK_APP_ID", "${getKey(keys, "FACEBOOK_APP_ID")}")
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    testOptions {
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile "org.robolectric:robolectric:3.1-SNAPSHOT"

    apt 'com.google.dagger:dagger-compiler:2.0.2'
    provided 'com.f2prateek.dart:dart-processor:2.0.0'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    compile('com.squareup.retrofit2:converter-simplexml:2.0.0-beta4') {
        exclude group: 'xpp3', module: 'xpp3'
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
    }
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.android.support:support-v4:23.3.0'
    compile 'com.android.support:support-annotations:23.3.0'
    compile 'com.android.support:percent:23.3.0'
    compile('com.crashlytics.sdk.android:answers:1.3.6@aar') {
        transitive = true;
    }
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
    compile 'com.f2prateek.dart:dart:2.0.0'
    compile 'com.facebook.android:facebook-android-sdk:4.10.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.google.android.gms:play-services-auth:8.4.0'
    compile 'com.google.dagger:dagger:2.0.2'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.squareup.okhttp3:okhttp:3.1.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'net.danlew:android.joda:2.9.2'
}

apply plugin: 'com.google.gms.google-services'